buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4"
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    }
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

sourceCompatibility = 1.7

group = 'com.athaydes.gradle.osgi'
version = '1.2.0'
description = 'The Gradle IPojo Plugin IPojoizes your jars, making it really easy to benefit from IPojo in any Gradle project'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'org.apache.felix:org.apache.felix.ipojo.manipulator:1.12.1'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task sourcesJar( type: Jar, dependsOn: classes ) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

task javadocJar( type: Jar ) {
    classifier "javadoc"
    from javadoc
}

/* Publishing config */

artifacts {
    archives javadocJar, sourcesJar
}

// add all the info required by Maven Central to the pom
configure( install.repositories.mavenInstaller ) {
    //repository(url: "file://$buildDir")
    pom.project {
        inceptionYear '2014'
        name project.name
        packaging 'jar'
        description project.description

        url 'https://github.com/renatoathaydes/osgi-run/tree/master/ipojo-plugin'

        scm {
            connection 'git@github.com:renatoathaydes/osgi-run.git'
            developerConnection 'git@github.com:renatoathaydes/osgi-run.git'
            url 'https://github.com/renatoathaydes/osgi-run'
        }

        licenses {
            license {
                name 'The Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'renatoathaydes'
                name 'Renato Athaydes'
                email 'renato@athaydes.com'
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/renatoathaydes/osgi-run/tree/master/ipojo-plugin'
    vcsUrl = 'https://github.com/renatoathaydes/osgi-run'

    //noinspection GroovyAssignabilityCheck
    plugins {
        osgiRunPlugin {
            id = 'com.athaydes.ipojo'
            displayName = 'IPojo Gradle Plugin'
            description = project.description
            tags = [ 'osgi', 'ipojo', 'modular' ]
        }
    }
}

apply plugin: 'com.jfrog.bintray'

bintray {
    user = project.hasProperty( "bintrayUserName" ) ? bintrayUserName : null
    key = project.hasProperty( "bintrayApiKey" ) ? bintrayApiKey : null
    configurations = [ 'archives' ]
    publish = true
    pkg {
        repo = 'maven'
        name = 'osgi-run-ipojo'
        licenses = [ 'Apache-2.0' ]
        desc = project.description
        websiteUrl = 'https://github.com/renatoathaydes/osgi-run'
        issueTrackerUrl = 'https://github.com/renatoathaydes/osgi-run/issues'
        vcsUrl = 'https://github.com/renatoathaydes/osgi-run.git'
        labels = [ 'osgi', 'ipojo', 'modular' ]
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }

    //noinspection GroovyAssignabilityCheck
    version {
        name = project.version
        vcsTag = project.version
        gpg {
            sign = true
        }
        mavenCentralSync {
            sync = true
            user = ossrhUsername
            password = ossrhPassword
            close = '1' // '0' to NOT close
        }
    }
}

bintrayUpload.dependsOn build, sourcesJar
publishPlugins.dependsOn build, sourcesJar
