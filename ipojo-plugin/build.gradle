buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5"
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    }
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'idea'

sourceCompatibility = 1.7

group = 'com.athaydes.gradle.osgi'
version = '1.1.1'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'org.apache.felix:org.apache.felix.ipojo.manipulator:1.12.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task sourcesJar( type: Jar, dependsOn: classes ) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

/* Publishing config */

publishing {
    publications {
        mavenJava( MavenPublication ) {
            from components.java
            artifact sourcesJar
        }
    }
}

apply plugin: 'com.jfrog.bintray'

bintray {
    user = project.hasProperty( "bintrayUserName" ) ? bintrayUserName : null
    key = project.hasProperty( "bintrayApiKey" ) ? bintrayApiKey : null
    publications = [ "mavenJava" ]
    publish = true
    pkg {
        repo = 'maven'
        name = 'osgi-run-ipojo'
        licenses = [ 'Apache-2.0' ]
        desc = 'The Gradle IPojo Plugin IPojoizes your jars, making it really easy to benefit from IPojo in any Gradle project.'
        websiteUrl = 'https://github.com/renatoathaydes/osgi-run'
        issueTrackerUrl = 'https://github.com/renatoathaydes/osgi-run/issues'
        labels = [ 'osgi', 'ipojo', 'modular' ]
        publicDownloadNumbers = true
    }
}

pluginBundle {
    website = 'https://github.com/renatoathaydes/osgi-run/tree/master/ipojo-plugin'
    vcsUrl = 'https://github.com/renatoathaydes/osgi-run'

    //noinspection GroovyAssignabilityCheck
    plugins {
        osgiRunPlugin {
            id = 'com.athaydes.ipojo'
            displayName = 'IPojo Gradle Plugin'
            description = 'The Gradle IPojo Plugin IPojoizes your jars, making it really easy to benefit from IPojo in any Gradle project.'
            tags = ['osgi', 'ipojo', 'modular']
        }
    }
}

bintrayUpload.dependsOn build, sourcesJar
publishPlugins.dependsOn build, sourcesJar