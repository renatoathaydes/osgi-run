plugins {
    id 'java'
    id 'com.athaydes.osgi-run'
    id "biz.aQute.bnd.builder" version "6.1.0"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.osgi:org.osgi.core:6.0.0'
    implementation 'org.apache.pdfbox:pdfbox:1.8.6'
}

jar {
    bundle {
        bnd( "Bundle-Name": "PdfBox Example",
                "Bundle-Vendor": "Renato Athaydes",
                "Bundle-Description": "Using non-bundle in OSGi Example",
                'Bundle-Activator': 'com.athaydes.osgirun.demo.pdfbox.PdfBoxDemo' )
    }
}.doLast {
    jar.manifest.writeTo( project.file( 'generated-sources/MANIFEST.MF' ) )
}

runOsgi {
    bundles += project

    wrapInstructions {
        printManifests = true
        manifest( /commons-logging.*/ ) {
            instruction 'Import-Package', '!javax.servlet', '!org.apache.*', '*'
            instruction 'Bundle-SymbolicName', 'org.apache.commons.logging'
        }
    }
}
